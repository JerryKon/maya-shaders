/*

Blended Texture shader

Used to set different colors in people, buildings...

*/

// CGFX MATRICES SECTION

float4x4 WorldViewProjection     : WorldViewProjection   < string UIWidget = "None"; >;
float4x4 WorldInverseTranspose   : WorldInverseTranspose < string UIWidget = "None"; >;
float4x4 ViewInverse             : ViewInverse           < string UIWidget = "None"; >;
float4x4 World                   : World                 < string UIWidget = "None"; >;


// Maya's CGFX Shader description
string description = "Blended Texture Shader";


// Parameters
float3 shaderColor : Diffuse
<
	string UIName = "Shader Color";
> = {1.0f, 1.0f, 1.0f};

float3 blendedColor : Diffuse
<
	string UIName = "Blended Color";
> = {0.0f, 0.0f, 1.0f};

float3 thirdColor : Diffuse
<
	string UIName = "Third Color";
> = {0.0f, 1.0f, 0.0f};

float3 lightDirection : Direction
<
	string UIName = "Light Direction";
> = {0.0f, 1.0f, 0.0f};

float3 ambientColor : Diffuse
<
	string UIName = "Ambient Color";
> = {0.20f, 0.20f, 0.20f};

float fresnelPower 
<
	string UIWidget = "slider";
	string UIName = "Fresnel Power";
	float UIMin = 0.0f;
	float UIMax = 10.0f;
	float UIStep = 0.01f;
	
> = 2.5f;

float specularPower 
<
	string UIWidget = "slider";
	string UIName = "Specular Power";
	float UIMin = 0.1f;
	float UIMax = 100.0f;
	float UIStep = 0.01f;
	
> = 30.0f;

float3 specularColor : Diffuse
<
	string UIName = "Specular Color";
> = {1.0f, 1.0f, 1.0f};

// TEXTURES
// Blend
texture blendTexure
<
	string name = "default_color.dds";
	string UIName = "Blend Texture";
	string TextureType = "2D";
>;

sampler2D blendMap = sampler_state
{
	Texture       = <blendTexure>;
    AddressU      = Wrap;
    AddressV      = Wrap;
};


// STRUCTS SECTION

// input from application
struct a2v {
	float4 position  : POSITION;
	float2 texCoord  : TEXCOORD0;
	float4 normal    : NORMAL;
};


// output to pixel/fragment program
struct v2f {
        float4 position        : POSITION;
		float2 texCoord        : TEXCOORD0;
		float3 worldNormal     : TEXCOORD1;
		float3 eyeVector	   : TEXCOORD2;
};


// PROGRAM SECTION

// Vertex Shader 
v2f vShader(a2v In)
{
    v2f Out;
	
    Out.position = mul(WorldViewProjection, In.position);
	Out.worldNormal = mul(WorldInverseTranspose, In.normal).xyz;
	
	float3 worldSpacePos = mul(World, In.position);
	float3 worldCameraPos = float3(ViewInverse[0].w, ViewInverse[1].w, ViewInverse[2].w); 
	Out.eyeVector = worldCameraPos - worldSpacePos;
	
	Out.texCoord = In.texCoord;
	
    return Out;
}

// Pixel Shader 
float4 pShader(v2f In) : COLOR
{
	float4 outColor;
	
	float3 worldNormal = normalize(In.worldNormal);
	float3 lightDir = normalize(-lightDirection);  
	float3 eyeVector = normalize(In.eyeVector);
	float3 reflectionVector = -reflect(eyeVector, worldNormal);

	// get texture colors
	float3 blend = tex2D(blendMap, In.texCoord).rgb;
	
	// set colors to textures rgb channels
	float3 finalColor = shaderColor * blend.r + blendedColor * blend.b + thirdColor * blend.g;

	float3 lambert = saturate(dot(lightDir, worldNormal));
	float3 specular = pow(saturate(dot(reflectionVector, lightDir)), specularPower) * specularColor;
	
	float3 fresnel = pow(1- saturate(dot(eyeVector, worldNormal)), fresnelPower) * (lambert + ambientColor);
	
	float3 finalSpec = fresnel + specular;
	
	outColor.rgb = (lambert + ambientColor) * finalColor + finalSpec ;
	
	outColor.a = fresnel;
		
	return outColor;
}


// TECHNIQUES SECTION

technique Opaque
{
    pass one
    {
		VertexShader = compile vp40 vShader();
		PixelShader = compile fp40 pShader();
    }
}